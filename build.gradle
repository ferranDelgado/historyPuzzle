buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "io.ratpack:ratpack-gradle:1.7.6"
    }
}

plugins {
    id "com.moowork.node" version "1.3.1"
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
}

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: "io.ratpack.ratpack-java"

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
}

repositories {
    mavenCentral()
}

wrapper {
    gradleVersion = '6.2.2'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

application {
    mainClassName = 'com.historypuzzle.AppKt'
}

node {
    // Version of node to use.
    version = '12.16.1'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true

    // Set the work directory for unpacking node
    workDir = file("${project.buildDir}/nodejs")

    // Set the work directory where node_modules should be located
    nodeModulesDir = file("${project.projectDir}/app")
}

task copyVueApp(type: Copy) {
    dependsOn yarn_build
    def currentDir = new File("").absolutePath
    from "$currentDir/app/dist"
    into "$currentDir/src/ratpack"
}

compileKotlin.dependsOn copyVueApp
